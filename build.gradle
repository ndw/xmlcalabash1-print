buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath fileTree(dir: 'lib').include("*.jar")
    classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion
    classpath group: 'biz.aQute.bnd', name: 'biz.aQute.bnd.gradle', version: '5.0.1'
  }
}

plugins {
  id "java"
  id "maven-publish"
  id "signing"
}

apply plugin: 'biz.aQute.bnd.builder'

repositories {
  // For javax.media:jai_core:1.1.3
  maven { url "https://maven.geotoolkit.org" }
  // For com.sun.media:jai-codec:1.1.3
  maven { url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/" }

  mavenLocal()
  mavenCentral()
  maven { url "https://maven.restlet.org" }
}

configurations {
  localLibs

  copydep {
    extendsFrom runtime
  }
  copydep.exclude module: 'xmlcalabash'
  copydep.exclude module: 'nwalsh-annotations'
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

dependencies {
  localLibs fileTree(dir: 'lib').include("*.jar")
  implementation (
    [group: 'com.nwalsh', name: 'nwalsh-annotations', version: '1.0.0'],
    [group: 'com.xmlcalabash', name: 'xmlcalabash', version: xmlCalabashVersion],
    [group: 'org.apache.xmlgraphics', name: 'fop', version: '2.4'],
    [group: 'xalan', name: 'xalan', version: '2.7.2'],
    [group: 'org.apache.avalon.framework', name: 'avalon-framework-api', version: '4.3.1'],
    [group: 'org.apache.avalon.framework', name: 'avalon-framework-impl', version: '4.3.1'],
    [group: 'javax.media', name: 'jai_core', version: '1.1.3'],
    [group: 'com.sun.media', name: 'jai-codec', version: '1.1.3']
  )
}

// Variables
project.ext.saxonRelease = saxonVersion.substring(0,5)
project.ext.saxonBranch  = saxonVersion.substring(0,3).replaceAll("\\.", "")
project.ext.releaseVersion = relVersion
project.ext.distVersion = relVersion + snapshot
relVersion = project.ext.distVersion // - gets turned into . in some contexts

compileJava {
  classpath += configurations.localLibs
  options.deprecation = true
  options.compilerArgs += ['-Xlint:deprecation']
  options.annotationProcessorPath = configurations.compileClasspath
}

javadoc {
  classpath += configurations.localLibs
}

task copyLib(type: Copy) {
  FileCollection lib = configurations.copydep
  String path = ""
  lib.each {
    File file -> path += " lib/" + file.name
  }
  project.ext.runtimeClasspath = path.trim()

  from lib
  into { "build/dist/lib" }
}

task copyCatalog(type: Copy) {
  from 'resources/catalog.xml'
  into 'build'
  filter { String line -> line.replaceAll('VERSION', distVersion) }
}

jar {
  archiveBaseName = "xmlcalabash1-print-" + relVersion
  from { ['build/catalog.xml', 'resources/library.xpl'] }
  bnd ('Bundle-Vendor': 'Norman Walsh',
       'Bundle-Description': 'XML Calabash paged media support.',
       'Bundle-Name': 'xmlcalabash1',
       'Bundle-SymbolicName': 'com.xmlcalabash',
       'Class-Path': project.ext.runtimeClasspath + " lib",
       'Implementation-Title': 'XML Calabash paged media support',
       'Implementation-Version': distVersion
  )
}
jar.dependsOn copyCatalog

task copyNotices(type: Copy) {
  from 'resources/notices'
  into 'build/dist/docs/notices'
}
jar.dependsOn copyNotices

task copyStuff(type: Copy) {
  from 'README.md'
  from 'LICENSE.md'
  into 'build/dist'
}

task copyToDist(dependsOn: [copyLib,copyStuff]) {
  // nop
}
jar.dependsOn copyToDist

task javadocJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/xmlcalabash1-print-" + distVersion + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task testStep(type: JavaExec) {
  classpath = configurations.copydep + configurations.runtimeClasspath + sourceSets.main.output

  main = 'com.xmlcalabash.drivers.Main'
  maxHeapSize = "1024m"
  args('-D', '-a', 'src/test/resources/test-pipeline.xpl')
}

task makeDist(dependsOn: [ build, copyJar, copyLib ]) {
  doLast {
    println("Created distribution in build/dist")
  }
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'xmlcalabash1-print-' + distVersion
  archiveFileName = 'xmlcalabash1-print-' + distVersion + ".zip"
}

task dist(dependsOn: zipDist) {
  // nop
}

signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'XML Calabash print support'
        packaging = 'jar'
        description = 'XML Calabash support for printing with FO and CSS'
        url = 'https://github.com/ndw/xmlcalabash1-print'

        scm {
          url = 'scm:git@github.com:ndw/xmlcalabash1-print.git'
          connection = 'scm:git@github.com:ndw/xmlcalabash1-print.git'
          developerConnection = 'scm:git@github.com:ndw/xmlcalabash1-print.git'
        }

        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }

      groupId = "com.xmlcalabash"
      artifactId = "xmlcalabash1-print"
      version = relVersion
      from components.java
      artifact javadocJar
      artifact sourcesJar
    }
  }

  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = sonatypeUsername
        password = sonatypePassword
      }
    }
  }
}
